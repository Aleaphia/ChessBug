plugins {
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

javafx {
    version = '23'
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.media' ]
}

jlink {
    launcher {
        name = 'chessbug'
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // This dependency is used by the application.
    implementation 'org.json:json:20250107'

    //Google Authenticator for TOTP authentication
    implementation 'com.warrenstrange:googleauth:1.5.0'
    
    // ZXing library for QR code generation 
    implementation 'com.google.zxing:core:3.5.1'
    implementation 'com.google.zxing:javase:3.5.1'

    //Javafx-Media for sounds
    implementation 'org.openjfx:javafx-media:23'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

sourceSets {
    main {
        java {
            srcDir "./src"
        }
        resources {
            srcDir "./src"
        }
    }
}

application {
    mainClass = 'chessBug.ChessBug'
    mainModule = 'chessbug'
}

task clientExample(dependsOn: classes, type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'chessBug.network.Example'
}

run {
    standardInput = System.in
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--add-reads', 'chessbug=ALL-UNNAMED']
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += ['--add-reads', 'chessbug=ALL-UNNAMED']
}

